plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def resolveThirdPartyDeps = !project.hasProperty('bundleThirdPartyDeps')

repositories {
    mavenCentral()
}

task installSmartClient(type: Copy) {
    description = 'Copy SmartClient Pro runtime and server JARs from distribution'
    group = 'setup'

    def scProDir = project.findProperty('scProDir')
    doFirst {
        if (scProDir == null) {
            throw new GradleException('Usage: gradle installSmartClient -PscProDir=<SmartClient Pro distribution root>')
        }
    }

    // Client runtime (excluding extra skins, reference, development)
    from("${scProDir}/smartclientRuntime/isomorphic") {
        exclude 'skins/**'
        exclude 'system/reference/**'
        exclude 'system/development/**'
        into 'src/main/webapp/isomorphic'
    }
    from("${scProDir}/smartclientRuntime/isomorphic/skins/Shiva") {
        into 'src/main/webapp/isomorphic/skins/Shiva'
    }
    from("${scProDir}/smartclientRuntime/isomorphic/skins/fonts") {
        into 'src/main/webapp/isomorphic/skins/fonts'
    }

    // Server JARs (Runtime + SDK)
    from("${scProDir}/smartclientRuntime/WEB-INF/lib") {
        if (resolveThirdPartyDeps) {
            include 'isomorphic_*.jar'
        }
        into 'lib'
    }
    from("${scProDir}/smartclientSDK/WEB-INF/lib") {
        include 'isomorphic_sql.jar'
        include 'isomorphic_tools.jar'
        include 'isomorphic_spring.jar'
        include 'isomorphic_autotest.jar'
        include 'isomorphic_contentexport.jar'
        if (!resolveThirdPartyDeps) {
            include 'commons-dbcp-1.4.jar'
        }
        into 'lib'
    }

    into layout.projectDirectory
}

task installAnalytics(type: Copy) {
    description = 'Copy Analytics module JS into client runtime'
    group = 'setup'

    def analyticsDir = project.findProperty('analyticsDir')
    doFirst {
        if (analyticsDir == null) {
            throw new GradleException('Usage: gradle installAnalytics -PanalyticsDir=<Analytics module root>')
        }
    }

    from(analyticsDir) {
        include 'ISC_Analytics.js'
        include 'ISC_Analytics.js.gz'
        into 'src/main/webapp/isomorphic/system/modules'
    }
    from("${analyticsDir}/modules-debug") {
        include 'ISC_Analytics.js'
        into 'src/main/webapp/isomorphic/system/modules-debug'
    }

    into layout.projectDirectory
}

task installRtm(type: Copy) {
    description = 'Copy RealtimeMessaging module JS and JARs'
    group = 'setup'

    def rtmDir = project.findProperty('rtmDir')
    doFirst {
        if (rtmDir == null) {
            throw new GradleException('Usage: gradle installRtm -PrtmDir=<RTM module root>')
        }
    }

    from(rtmDir) {
        include 'ISC_RealtimeMessaging.js'
        include 'ISC_RealtimeMessaging.js.gz'
        into 'src/main/webapp/isomorphic/system/modules'
    }
    from("${rtmDir}/modules-debug") {
        include 'ISC_RealtimeMessaging.js'
        into 'src/main/webapp/isomorphic/system/modules-debug'
    }
    from(rtmDir) {
        include '*.jar'
        into 'lib'
    }

    into layout.projectDirectory
}

task installAi(type: Copy) {
    description = 'Copy AI module JS and JARs'
    group = 'setup'

    def aiDir = project.findProperty('aiDir')
    doFirst {
        if (aiDir == null) {
            throw new GradleException('Usage: gradle installAi -PaiDir=<AI module root>')
        }
    }

    from(aiDir) {
        include 'ISC_AI.js'
        include 'ISC_AI.js.gz'
        into 'src/main/webapp/isomorphic/system/modules'
    }
    from("${aiDir}/modules-debug") {
        include 'ISC_AI.js'
        into 'src/main/webapp/isomorphic/system/modules-debug'
    }
    from(aiDir) {
        include '*.jar'
        into 'lib'
    }

    into layout.projectDirectory
}

configurations.all {
    exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'jakarta.servlet.jsp:jakarta.servlet.jsp-api:4.0.0'

    if (resolveThirdPartyDeps) {
        implementation fileTree(dir: 'lib', include: 'isomorphic_*.jar')

        // Mail / Activation
        implementation 'org.eclipse.angus:angus-activation:2.0.2'
        implementation 'org.eclipse.angus:angus-core:2.0.3'
        implementation 'org.eclipse.angus:angus-mail:2.0.3'
        implementation 'org.eclipse.angus:smtp:2.0.3'

        // Commons
        implementation 'commons-cli:commons-cli:1.4'
        implementation 'commons-codec:commons-codec:1.15'
        implementation 'commons-collections:commons-collections:3.2.2'
        implementation 'org.apache.commons:commons-collections4:4.4'
        implementation 'org.apache.commons:commons-compress:1.21'
        implementation 'org.apache.commons:commons-fileupload2-core:2.0.0-M2'
        implementation 'org.apache.commons:commons-fileupload2-jakarta-servlet5:2.0.0-M2'
        implementation 'commons-io:commons-io:2.16.1'
        implementation 'commons-jxpath:commons-jxpath:1.3'
        implementation 'commons-lang:commons-lang:2.6'
        implementation 'org.apache.commons:commons-lang3:3.11'
        implementation 'org.apache.commons:commons-math3:3.6.1'
        implementation 'commons-pool:commons-pool:1.6'
        implementation 'org.apache.commons:commons-vfs2:2.7.0'
        implementation 'commons-dbcp:commons-dbcp:1.4'

        // Template / Scripting
        implementation 'org.freemarker:freemarker:2.3.33'
        implementation 'org.apache.groovy:groovy:4.0.28'
        implementation 'org.apache.groovy:groovy-jsr223:4.0.28'
        implementation 'org.apache.groovy:groovy-sql:4.0.28'
        implementation 'org.apache.velocity:velocity-engine-core:2.3'
        implementation 'org.openjdk.nashorn:nashorn-core:15.4'

        // Validation
        implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
        implementation 'javax.validation:validation-api:1.0.0.GA'

        // HTTP
        implementation 'org.apache.httpcomponents:httpclient:4.5.13'
        implementation 'org.apache.httpcomponents:httpcore:4.4.14'

        // Jakarta / JAXB
        implementation 'com.sun.istack:istack-commons-runtime:4.2.0'
        implementation 'jakarta.activation:jakarta.activation-api:2.1.3'
        implementation 'jakarta.mail:jakarta.mail-api:2.1.3'
        implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
        implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
        implementation 'org.glassfish.jaxb:jaxb-core:4.0.5'
        implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

        // Logging
        runtimeOnly 'org.apache.logging.log4j:log4j-api:2.17.1'
        runtimeOnly 'org.apache.logging.log4j:log4j-core:2.17.1'

        // Misc
        implementation 'org.apache.ant:ant:1.10.15'
        implementation 'joda-time:joda-time:2.10.8'
        implementation 'org.apache.poi:poi:4.1.2'
        implementation 'org.apache.poi:poi-ooxml:4.1.2'
        implementation 'org.apache.poi:poi-ooxml-schemas:4.1.2'
        implementation 'com.zaxxer:SparseBitSet:1.2'
        implementation 'org.apache.xmlbeans:xmlbeans:3.1.0'
    } else {
        implementation fileTree(dir: 'lib', include: '*.jar',
            exclude: ['log4j-slf4j-impl-*.jar', 'slf4j-api-*.jar'])
    }

    runtimeOnly 'org.hsqldb:hsqldb'
}
